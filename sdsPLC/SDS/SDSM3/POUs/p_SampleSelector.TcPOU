<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4018.5">
  <POU Name="p_SampleSelector" Id="{77fcb81f-0178-4f6b-9677-2217e0eaf57e}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM p_SampleSelector
VAR
	
	fbSelectorSync	:	FB_SelectorSync;
	
	afbViciDriver : ARRAY [1..4] OF FB_ViciDriver;
	
	fbL01Driver			:	FB_L01Driver;
	fbSensirionDriver	:	FB_SensirionDriver;
	

	stStatus: STRING;
	udCount: UDINT;

	tofReset: TOF;

	iIdOld	:	INT;
	
	index: INT := 1;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Assign addresses on first pass *)
IF g_xFirstPass THEN
stSelector.stViciVlvCtrl[1].iAddress := 1;
stSelector.stViciVlvCtrl[2].iAddress := 2;


END_IF

stSelector.iId := 1020; // added for CXI SPI deployment. Ideally this will be automated in the future.

(* Flowmeter Readback *)
(* Note: Flow should be delivered in units of uL/min *)
(* If the selector changed, reset flow meter *)
(*
IF iIdOld<>stSelector.iId THEN
	stSelector.xFMReset := TRUE; // set here, reset in the driver
	iIdOld:=stSelector.iId; // After we've checked, update old Id holder
END_IF
*)
(* Run flowmeter driver based on which selector is attached *)
CASE stSelector.iId OF
	1020:
		Sensirion();
		stSelector.iFlowMeterType := 1;
	1021:
		Bronkhorst();
		stSelector.iFlowMeterType := 2;
	1022:
		Sensirion();
		stSelector.iFlowMeterType := 1;
ELSE // If the selector is not recognized we have a problem.
	stSelector.iFlowMeterType:=0;
	stSelector.rFlow := 0;
	stSelector.iFMState := 3;//invalid
END_CASE


(* Valve control *)

fbSelectorSync(iq_stSelector:=stSelector);


stSelector.stViciVlvCtrl[1].iReqPos := LIMIT(1, stSelector.iVici1ReqPos, 12);
stSelector.stViciVlvCtrl[2].iReqPos := LIMIT(1, stSelector.iVici2ReqPos, 12);


(* Run valve drivers *)
afbViciDriver[index](
	i_xExecute:= TRUE,
	i_tTimeOut:= t#10s,
	i_stControl:= stSelector.stViciVlvCtrl[index],
	iq_stSerialRXBuffer:= SerialRXBuffer_SelVici,
	iq_stSerialTXBuffer:= SerialTXBuffer_SelVici,
	q_stStatus=>stSelector.stViciVlvStatus[index]);

IF 	afbViciDriver[index].q_xDone OR
	afbViciDriver[index].q_xError OR
	afbViciDriver[index].q_xTimeout THEN
	(* reset function for next time *)
	afbViciDriver[index](i_xExecute:=FALSE, iq_stSerialRXBuffer:= SerialRXBuffer_SelVici, iq_stSerialTXBuffer:=  SerialTXBuffer_SelVici);
	
	index := index + 1;
	IF index > 2 THEN index := 1; END_IF
	
END_IF

//for epics readback to be consistent
IF stSelector.xResSyncd THEN stSelector.iSyncResPos := stSelector.stViciVlvStatus[1].iCurrPos; END_IF

(* Shakers *)
stSelector.stShaker01.q_xPwrDO := stSelector.stShaker01.i_xEpics;
stSelector.stShaker02.q_xPwrDO := stSelector.stShaker02.i_xEpics;
stSelector.stShaker03.q_xPwrDO := stSelector.stShaker03.i_xEpics;
stSelector.stShaker04.q_xPwrDO := stSelector.stShaker04.i_xEpics;]]></ST>
    </Implementation>
    <Action Name="Sensirion" Id="{bddd2bef-c943-42c2-b99c-c3cd7513c804}">
      <Implementation>
        <ST><![CDATA[(* Sensirion Flowmeter driver encapsulation*)	

// Set flowmeter type
stSelector.iFlowMeterType := 1;

(* Reinitialize flowmeter *)
stSelector.stSelFMControlSens.xReset := stSelector.xFMReset;
stSelector.stSelFMControlSens.xCalMode := stSelector.xFMModeReq;



fbSensirionDriver(
	i_xExecute:= TRUE,
	i_tTimeOut:= t#10s,
	iq_stSerialRXBuffer:= SerialRXBuffer_SelFlwMtr,
	iq_stSerialTXBuffer:= SerialTXBuffer_SelFlwMtr,
	q_stStatus=>stSelector.stSelFMStatusSens,
	i_stControl:=stSelector.stSelFMControlSens);

IF fbSensirionDriver.q_xDone OR
	fbSensirionDriver.q_xError OR
	fbSensirionDriver.q_xTimeout THEN
		
		stSelector.rFlow := stSelector.stSelFMStatusSens.rFlow;
		stSelector.xFMOoR := stSelector.stSelFMStatusSens.xOoR;
		stSelector.xFlowValid := stSelector.stSelFMStatusSens.xFlowValid;
		stSelector.iFMState := stSelector.stSelFMStatusSens.iState;
		stSelector.xFMModeRb := stSelector.stSelFMStatusSens.xFMMode;
		
	
		(* reset function for next time *)
		fbSensirionDriver(i_xExecute:=FALSE, iq_stSerialRXBuffer:= SerialRXBuffer_SelFlwMtr, iq_stSerialTXBuffer:=  SerialTXBuffer_SelFlwMtr);	
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="Bronkhorst" Id="{eba5bca7-954d-4178-b56c-f4810b966e4b}">
      <Implementation>
        <ST><![CDATA[(* Bronkhorst Flowmeter driver encapsulation *)

//Set flowmeter type
stSelector.iFlowMeterType := 2;

(* Reinitialize flowmeter *)
//Note, the bronkhorst does not have any initialization process.
//stSelector.stSelFMControlBh.xReset := stSelector.xFMReset;
		
fbL01Driver(
	i_xExecute:= TRUE,
	i_tTimeOut:= t#10s,
	iq_stSerialRXBuffer:= SerialRXBuffer_SelFlwMtr,
	iq_stSerialTXBuffer:= SerialTXBuffer_SelFlwMtr,
	q_stStatus=>stSelector.stSelFMStatusBh,
	i_stControl:=stSelector.stSelFMControlBh);

IF fbL01Driver.q_xDone OR
	fbL01Driver.q_xError OR
	fbL01Driver.q_xTimeout THEN
		
		stSelector.rFlow := stSelector.stSelFMStatusBh.rFlow;
		stSelector.xFMOoR := stSelector.stSelFMStatusBh.xOoR;
		stSelector.xFlowValid := stSelector.stSelFMStatusBh.xFlowValid;
		stSelector.iFMState	:= stSelector.stSelFMStatusBh.iState;
	
	(* reset function for next time *)
	fbL01Driver(i_xExecute:=FALSE, iq_stSerialRXBuffer:= SerialRXBuffer_SelFlwMtr, iq_stSerialTXBuffer:=  SerialTXBuffer_SelFlwMtr);	
END_IF]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>