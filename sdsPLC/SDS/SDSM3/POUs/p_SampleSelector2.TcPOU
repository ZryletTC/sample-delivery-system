<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4018.5">
  <POU Name="p_SampleSelector2" Id="{b0faa8f5-9b93-47b6-893c-6fbd4af9adfd}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM p_SampleSelector2
VAR
	
	fbSelectorSync	:	FB_SelectorSync;
	
	afbViciDriver : ARRAY [1..4] OF FB_ViciDriver;
	
	fbL01Driver	:	FB_L01Driver;
	fbSensirionDriver2	:	FB_SensirionDriver;

	stStatus: STRING;
	udCount: UDINT;

	tofReset: TOF;

	
	index: INT := 1;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Assign addresses on first pass *)
IF g_xFirstPass THEN
stSelector2.stViciVlvCtrl[1].iAddress := 1;
stSelector2.stViciVlvCtrl[2].iAddress := 2;
END_IF

stSelector2.iId := 1020; // added for CXI SPI deployment. Ideally this will be automated in the future.

(* Flowmeter Readback *)
(* Note: Flow should be delivered in units of uL/min *)
(* If the selector changed, reset flow meter *)
(*
IF iIdOld<>stSelector.iId THEN
	stSelector.xFMReset := TRUE; // set here, reset in the driver
	iIdOld:=stSelector.iId; // After we've checked, update old Id holder
END_IF
*)
(* Run flowmeter driver based on which selector is attached *)
CASE stSelector2.iId OF
	1020:
		Sensirion();
		stSelector2.iFlowMeterType := 1;
	1021:
		Bronkhorst();
		stSelector2.iFlowMeterType := 2;
	1022:
		Sensirion();
		stSelector2.iFlowMeterType := 1;
ELSE // If the selector is not recognized we have a problem.
	stSelector2.iFlowMeterType:=0;
	stSelector2.rFlow := 0;
	stSelector2.iFMState := 3;//invalid
END_CASE



(* Valve control *)

fbSelectorSync(iq_stSelector:=stSelector2);


stSelector2.stViciVlvCtrl[1].iReqPos := LIMIT(1, stSelector2.iVici1ReqPos, 12);
stSelector2.stViciVlvCtrl[2].iReqPos := LIMIT(1, stSelector2.iVici2ReqPos, 12);


(* Run driver *)
afbViciDriver[index](
	i_xExecute:= TRUE,
	i_tTimeOut:= t#10s,
	i_stControl:= stSelector2.stViciVlvCtrl[index],
	iq_stSerialRXBuffer:= SerialRXBuffer_Sel2Vici,
	iq_stSerialTXBuffer:= SerialTXBuffer_Sel2Vici,
	q_stStatus=>stSelector2.stViciVlvStatus[index]);

IF 	afbViciDriver[index].q_xDone OR
	afbViciDriver[index].q_xError OR
	afbViciDriver[index].q_xTimeout THEN
	(* reset function for next time *)
	afbViciDriver[index](i_xExecute:=FALSE, iq_stSerialRXBuffer:= SerialRXBuffer_Sel2Vici, iq_stSerialTXBuffer:=  SerialTXBuffer_Sel2Vici);
	
	index := index + 1;
	IF index > 2 THEN index := 1; END_IF
	
END_IF

//for epics readback to be consistent
IF stSelector2.xResSyncd THEN stSelector2.iSyncResPos := stSelector2.stViciVlvStatus[1].iCurrPos-1; END_IF


(* Shakers *)
stSelector2.stShaker01.q_xPwrDO := stSelector2.stShaker01.i_xEpics;
stSelector2.stShaker02.q_xPwrDO := stSelector2.stShaker02.i_xEpics;
stSelector2.stShaker03.q_xPwrDO := stSelector2.stShaker03.i_xEpics;
stSelector2.stShaker04.q_xPwrDO := stSelector2.stShaker04.i_xEpics;]]></ST>
    </Implementation>
    <Action Name="Sensirion" Id="{0ca88dc7-d299-44a5-8de3-aa91a7761f69}">
      <Implementation>
        <ST><![CDATA[(* Sensirion Flowmeter driver encapsulation*)	

// Set flowmeter type
stSelector2.iFlowMeterType := 1;

(* Reinitialize flowmeter *)
stSelector2.stSelFMControlSens.xReset := stSelector2.xFMReset;
stSelector2.stSelFMControlSens.xCalMode := stSelector2.xFMModeReq;



fbSensirionDriver2(
	i_xExecute:= TRUE,
	i_tTimeOut:= t#10s,
	iq_stSerialRXBuffer:= SerialRXBuffer_Sel2FlwMtr,
	iq_stSerialTXBuffer:= SerialTXBuffer_Sel2FlwMtr,
	q_stStatus=>stSelector2.stSelFMStatusSens,
	i_stControl:=stSelector2.stSelFMControlSens);

IF fbSensirionDriver2.q_xDone OR
	fbSensirionDriver2.q_xError OR
	fbSensirionDriver2.q_xTimeout THEN
		
		stSelector2.rFlow := stSelector2.stSelFMStatusSens.rFlow;
		stSelector2.xFMOoR := stSelector2.stSelFMStatusSens.xOoR;
		stSelector2.xFlowValid := stSelector2.stSelFMStatusSens.xFlowValid;
		stSelector2.iFMState := stSelector2.stSelFMStatusSens.iState;
		stSelector2.xFMModeRb := stSelector2.stSelFMStatusSens.xFMMode;
		
	
		(* reset function for next time *)
		fbSensirionDriver2(i_xExecute:=FALSE, iq_stSerialRXBuffer:= SerialRXBuffer_Sel2FlwMtr, iq_stSerialTXBuffer:=  SerialTXBuffer_Sel2FlwMtr);	
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="Bronkhorst" Id="{eee8ceac-55f7-4943-8de4-400a89888e62}">
      <Implementation>
        <ST><![CDATA[(* Bronkhorst Flowmeter driver encapsulation *)

//Set flowmeter type
stSelector2.iFlowMeterType := 2;

(* Reinitialize flowmeter *)
//Note, the bronkhorst does not have any initialization process.
//stSelector.stSelFMControlBh.xReset := stSelector.xFMReset;
		
fbL01Driver(
	i_xExecute:= TRUE,
	i_tTimeOut:= t#10s,
	iq_stSerialRXBuffer:= SerialRXBuffer_Sel2FlwMtr,
	iq_stSerialTXBuffer:= SerialTXBuffer_Sel2FlwMtr,
	q_stStatus=>stSelector2.stSelFMStatusBh,
	i_stControl:=stSelector2.stSelFMControlBh);

IF fbL01Driver.q_xDone OR
	fbL01Driver.q_xError OR
	fbL01Driver.q_xTimeout THEN
		
		stSelector2.rFlow := stSelector2.stSelFMStatusBh.rFlow;
		stSelector2.xFMOoR := stSelector2.stSelFMStatusBh.xOoR;
		stSelector2.xFlowValid := stSelector2.stSelFMStatusBh.xFlowValid;
		stSelector2.iFMState	:= stSelector2.stSelFMStatusBh.iState;
	
	(* reset function for next time *)
	fbL01Driver(i_xExecute:=FALSE, iq_stSerialRXBuffer:= SerialRXBuffer_Sel2FlwMtr, iq_stSerialTXBuffer:=  SerialTXBuffer_Sel2FlwMtr);	
END_IF]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>